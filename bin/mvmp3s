#!/usr/bin/env perl

use warnings;
use strict;
use Cwd qw/cwd/;
use File::chdir;
use File::Copy qw/move/;
use File::Find;
use File::Path qw/make_path/;
use Getopt::Long;
use List::Util qw/uniq/;

my $START_DIR = cwd();
my($verbose, $dry_run, $no_dropbox_deletion);

GetOptions(
  'dry_run|n'           => \$dry_run,
  'no_dropbox_deletion' => \$no_dropbox_deletion,
  'verbose'             => \$verbose,
);

$verbose = 1; # Hard-coded for now for debugging purposes

my $DROPBOX_BASE_DIR = qq{$ENV{HOME}/Dropbox/Music};
my $AUDIO_FILE_BASE_DIR = qq{$ENV{HOME}/Music/Phile Audio/audio/test};

my %created_dropbox_dirs;

my @directories;
find(\&wanted, $AUDIO_FILE_BASE_DIR);

@directories = sort {$a->{shortname} cmp $b->{shortname}} @directories;
print "Will process directories in this order: \n\t", join("\n\t", map {$_->{name}} @directories), "\n" if $verbose and @directories;

foreach my $dir (@directories) {
    process_disc_directory($dir);
}

$CWD = $START_DIR;


sub wanted {
    return unless -d;

    return unless $File::Find::name =~ m{.*/([^/]+)/([^/]+)/Disc\s+(\d+)$};
    my($artist, $album, $disc_number) = ($1, $2, $3);

    $artist =~ s/[']/_/g;
    $album =~ s/[']/_/g;

    $artist =~ s/&/ and /g;
    $album =~ s/&/ and /g;

    $artist =~ s/\s+/_/g;
    $album =~ s/\s+/_/g;

    push(@directories, {
        album       => $album,
        artist      => $artist,
        dir         => $File::Find::dir,
        disc_number => $disc_number,
        disc_count  => how_many_discs($File::Find::dir),
        name        => $File::Find::name,
        shortname   => $_,
    });
}

sub process_disc_directory {
    my $dir = shift;

    $CWD = $dir->{name};

    my($music_files, $file_types) = find_music_files($dir->{name});
    print "$dir->{name} : $dir->{artist} || $dir->{album} || $dir->{disc_number}/$dir->{disc_count} || ",
        join(', ', @$file_types), "\n" if $verbose;

    foreach my $type (@$file_types) {
        my $dropbox_path = prepare_directory($dir->{artist}, $dir->{album}, $type);
        foreach my $filename (grep /[.]$type$/, @$music_files) {
            print "Processing $filename to $dropbox_path\n" if $verbose;
            my $fixed_filename = fix_filename($filename, $dir->{disc_number}, $dir->{disc_count});

            print "Filename is now $fixed_filename\n" if $verbose;
            move_file($fixed_filename, $dropbox_path);
            print "\n" if $verbose;
        }
    }

    $CWD = $START_DIR;
    print "Kicking off the process to remove $dir->{name}\n" if $verbose;
    remove_album_directory($dir->{name});
}

sub remove_album_directory {
    my($full_path) = @_;

    my $max_directory_cleanout = 3;
    my $directory_path = $full_path;
    while($max_directory_cleanout-- > 0) {
        print qq{Looking to remove $directory_path...\n} if $verbose;
        opendir CURRENT_DIR, $directory_path or die "Couldn't open directory $directory_path: $!";
        my @files = grep /^[^.]/, readdir CURRENT_DIR;
        closedir CURRENT_DIR;

        print qq{Found files: @files\n};
        last if @files;

        print qq{rm -rf $directory_path\n} if $verbose;
        `rm -rf '$directory_path'` unless $dry_run;

        $directory_path =~ s{/[^/]+$}{};
    }
}

sub fix_filename {
    my($filename, $disc_number, $number_of_discs) = @_;

    my $is_part_of_multi_disc = $number_of_discs > 1;
    print '     ', ($is_part_of_multi_disc ? 'is ' : 'is not '), "part of a multi-disc collection\n" if $verbose;

    my $cwd = cwd();
    my $command = q{mp3capitalize --bare-print } . ($dry_run ? q{--dry-run } : '') . qq{"$filename"};
    print qq{Running `$command` in $cwd\n} if $verbose;
    my $new_filename = `$command`;
    print "Filename is now $new_filename\n" if $verbose;

    if($is_part_of_multi_disc) {
        my $disc_filename = $new_filename;
        $disc_filename =~ s{^}{Disc_$disc_number-};
        print "Would rename $new_filename to $disc_filename\n" if $verbose;
        move($new_filename, $disc_filename) unless $dry_run;

        $new_filename = $disc_filename;
    }

    return $new_filename;
}

sub how_many_discs {
    my $album_directory = shift;

    print "Looking for how many discs in $album_directory\n" if $verbose;

    opendir CURRENT_DIR, $album_directory or die "Couldn't open directory $album_directory: $!";
    my @files = readdir CURRENT_DIR;
    closedir CURRENT_DIR;

    my $count = scalar(grep /Disc/, @files);
    print "Found $count Disc directories among ", join(', ', @files), "\n" if $verbose;
    return $count;
}

sub find_music_files {
    my $disc_directory = shift;

    opendir CURRENT_DIR, $disc_directory or die "Couldn't open directory $disc_directory: $!";
    my @music_files = readdir CURRENT_DIR;
    closedir CURRENT_DIR;

    @music_files = grep /flac|mp3|ogg/, @music_files;
    my @file_types = uniq grep /flac|mp3|ogg/, map {(split /[.]/)[-1]} @music_files;

    return \@music_files, \@file_types;
}

sub move_file {
    my($filename, $dropbox_path) = @_;

    my $new_path = join '/', $dropbox_path, $filename;
    print "rename $filename, $new_path\n" if $verbose;
    move($filename, $new_path) unless $dry_run;
}

sub prepare_directory {
    my($artist, $album, $extension) = @_;
    print "Preparing directory for $extension/$artist/$album\n";

    die unless $artist and $album and $extension;

    my $dropbox_path = qq{$DROPBOX_BASE_DIR/$extension/$artist/$album};

    if(-d $dropbox_path and not $created_dropbox_dirs{$dropbox_path} and not $no_dropbox_deletion) {
        print "$dropbox_path already exists, deleting...\n" if $verbose;
        `rm -rf $dropbox_path` unless $dry_run;
    }

    if(not -d $dropbox_path) {
        $created_dropbox_dirs{$dropbox_path}++;

        print "Making directory $dropbox_path\n" if $verbose;
        make_path($dropbox_path) unless $dry_run;
    }

    return $dropbox_path;
}
