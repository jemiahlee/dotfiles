#!/usr/bin/perl

use warnings;
use strict;

use Getopt::Long;
use Scalar::Util qw/openhandle/;

my($verbose, $delimiter);
$delimiter = '';

GetOptions(
    'delimiter=s' => \$delimiter,
    'verbose'     => \$verbose,
);

my @filehandles;
foreach my $argv (@ARGV) {
    open my $fh, '<', $argv or die "Couldn't open $argv: $!";
    push @filehandles, $fh;
}

while(grep {openhandle($_)} @filehandles) {
    my @outputs;

    foreach my $fh (@filehandles) {
        unless(openhandle($fh)) {
            push @outputs, '';
            next;
        }

        my $readline = <$fh>;

        unless(defined $readline) {
            $fh->close();
            push @outputs, '';
            next;
        }

        chomp $readline;
        push @outputs, $readline;
    }

    print(join($delimiter, @outputs), "\n") if grep {$_} @outputs;
}
